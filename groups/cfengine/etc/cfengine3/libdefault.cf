
body perms promisemode(p)
{
    mode  => "$(p)";
}

body contain in_dir_and_setuid_sh(dir,owner)
{
    chdir => "$(dir)";
    exec_owner => "$(owner)";
    useshell => "true";
}

bundle agent download_tarball(tarballurl, tarballdst ){
# Gets a tarball or any file from a given url
# ex : 
#        tarballurl = "https://github.com/gothfox/Tiny-Tiny-RSS/archive/1.8.tar.gz"
#        tarballdst = "/root/ttrss-latest.tar.gz"

classes:
    "tarball_is_present" expression => fileexists("$(tarballdst)");

commands:
    !tarball_is_present::
        "/usr/bin/wget -q -O $(tarballdst) $(tarballurl)"
        comment => "Downloading Tarball from $(tarballurl).";
}


bundle agent pkg_install_if_no_file(package, file)
{
# Checks if a file exists
# Install a package if not
classes:
    "file_exists" expression => fileexists("$(file)");

reports:
    !file_exists::
        "File $(file) does not exists, installing $(package).";

packages:
    !file_exists.(debian|redhat)::
        "$(package)"
        comment => "install $(package)",
        action => if_elapsed("240"),
        package_policy   => "add",
        package_method   => generic;
}
