# Voir : http://sysadvent.blogspot.fr/2011/12/day-15-automating-wordpress-with.html
bundle agent app_ttrss {
classes:
    "need_lighttpd" or => {"tinytinyrss"};
    "need_mysql" or => {"tinytinyrss"};

methods:
    tinytinyrss::
        "get_ttrss"         usebundle => tinytinyrss_tarball_is_present;
        "extract_ttrss"     usebundle => tinytinyrss_tarball_extract;
        "database_ttrss"    usebundle => configuration_of_mysql_db_for_ttrss;
    
    need_mysql::
        "handle_mysql"   usebundle => fix_mysql;

    need_lighttpd::
        "handle_lighttpd"   usebundle => fix_lighttpd;


files:
    tinytinyrss.need_lighttpd::
        "/etc/lighttpd/conf-enabled/99-redirect-rss.conf"
            handle => "fix_lighttpd_ttrss_redirect",
            comment => "Append Lighttpd redirect",
            perms => mog("0644","root","root"),
            create => "true",
            edit_line => append_if_no_lines('url.redirect = ("^/$" => "Tiny-Tiny-RSS-1.8/" )'),
            classes => if_repaired("reload_lighttpd"); 

commands:
    reload_lighttpd::
        "/etc/init.d/lighttpd force-reload";
}

bundle agent fix_mysql {
classes:

    "installed_mysql" expression => fileexists("/usr/bin/mysql");
vars:
    "mysqlutils" slist => { "mysql-server" };

packages:
    !installed_mysql::
        "$(mysqlutils)"
        comment => "install $(mysqlutils)",
                action => if_elapsed("240"),
                package_policy   => "add",
                package_method   => generic;
}

bundle agent fix_lighttpd {
classes:

    "installed_lighttpd" expression => fileexists("/usr/sbin/lighttpd");
    "installed_php5cgi" expression => fileexists("/usr/bin/php5-cgi");
    "installed_php5mysql" expression => fileexists("/usr/share/doc/php5-mysql/copyright");
    
    installed_lighttpd::
        "enabled_php5cgi" expression => fileexists("$(fascgiphp_conf)");
        "enabled_fastcgi" expression => fileexists("$(fascgi_conf)");

vars:
        "fascgiphp_conf" string => "/etc/lighttpd/conf-enabled/15-fastcgi-php.conf";
        "fascgi_conf" string => "/etc/lighttpd/conf-enabled/10-fastcgi.conf";
        "lighttpdutils" slist => { "lighttpd", "php5-cgi", "php5-mysql", "php5-curl" };

files:
    installed_lighttpd::
        "/etc/lighttpd/lighttpd.conf"
            handle => "fix_lighttpd_conf",
            comment => "Fix Lighttpd config file",
            perms => mog("0644","root","root"),
            edit_line => replace_line_end(
                    "static-file.exclude-extensions", 
                    '= ( ".pl", ".fcgi" )'),
            classes => if_repaired("reload_lighttpd"); 


packages:
    !(installed_lighttpd.installed_php5cgi.installed_php5mysql)::
        "$(lighttpdutils)"
        comment => "install $(lighttpdutils)",
                action => if_elapsed("240"),
                package_policy   => "add",
                package_method   => generic;

commands:
    reload_lighttpd::
        "/etc/init.d/lighttpd force-reload";

    !enabled_fastcgi::
        "/usr/sbin/lighty-enable-mod fastcgi"
            classes => if_repaired("reload_lighttpd");

    !enabled_php5cgi::
        "/usr/sbin/lighty-enable-mod fastcgi-php"
            classes => if_repaired("reload_lighttpd"); 

}

bundle agent tinytinyrss_tarball_is_present {
vars:
    "ttrss_tarball" string => "/root/ttrss-latest.tar.gz";
    "ttrss_url" string => "https://github.com/gothfox/Tiny-Tiny-RSS/archive/1.8.tar.gz";

classes:
      "ttrss_tarball_is_present" expression =>
              fileexists("$(ttrss_tarball)");

reports:
        ttrss_tarball_is_present::
                "TinytinyRSS tarball is on disk.";

commands:
          !ttrss_tarball_is_present::
                  "/usr/bin/wget -q -O $(ttrss_tarball) $(ttrss_url)"
                      comment => "Downloading WordPress.";
}

bundle agent tinytinyrss_tarball_extract {
vars:
    "ttrss_tarball" string => "/root/ttrss-latest.tar.gz";
    "ttrss_destination" string => "/var/www";
    "ttrss_directory" string => "$(ttrss_destination)/Tiny-Tiny-RSS-1.8";

classes:
    "ttrss_directory_present"  expression => fileexists("$(ttrss_directory)/");
    "ttrss_tarball_present"  expression => fileexists("$(ttrss_tarball)");

files:
    "$(ttrss_directory)"
        perms => mog("0644","www-data","www-data"),
        depth_search => recurse("inf");

reports:
    ttrss_directory_present::
        "Ttrss directory is present.";

commands:
    !ttrss_directory_present.ttrss_tarball_present::
        "/bin/tar -C $(ttrss_destination) -xvzf $(ttrss_tarball)"
            comment => "Unrolling tarball to $(ttrss_destination).";

}

bundle agent configuration_of_mysql_db_for_ttrss
{
vars:
    "database" string => "ttrss";
    "user" string => "myttrssuser";
    "password" string => "TinyRSS@PassWord";

commands:
    "/usr/bin/mysql -u root -e \"
              CREATE DATABASE IF NOT EXISTS ttrss;
          GRANT ALL PRIVILEGES ON $(database).*
                  TO '$(user)'@localhost
                      IDENTIFIED BY '$(password)';
              FLUSH PRIVILEGES;\"
                    ";
}
