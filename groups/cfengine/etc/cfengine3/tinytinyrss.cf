# Voir : http://sysadvent.blogspot.fr/2011/12/day-15-automating-wordpress-with.html
bundle agent app_ttrss {
vars:
    tinytinyrss::
        "ttrss_tarball" string => "/root/ttrss-latest.tar.gz";
        "ttrss_url" string => "https://github.com/gothfox/Tiny-Tiny-RSS/archive/1.8.tar.gz";
classes:
    "need_lighttpd" or => {"tinytinyrss"};
    "need_mysql" or => {"tinytinyrss"};

methods:
    tinytinyrss::
        "get_ttrss"         usebundle => download_tarball(
                "$(ttrss_url)", "$(ttrss_tarball)");
        "extract_ttrss"     usebundle => tinytinyrss_tarball_extract;
        "database_ttrss"    usebundle => configuration_of_mysql_db_for_ttrss;
    
    need_mysql::
        "handle_mysql"   usebundle => pkg_install_if_no_file(
                "mysql-server", "/usr/bin/mysql";

    need_lighttpd::
        "handle_lighttpd"   usebundle => fix_lighttpd;


files:
    tinytinyrss.need_lighttpd::
        "/etc/lighttpd/conf-enabled/99-redirect-rss.conf"
            handle => "fix_lighttpd_ttrss_redirect",
            comment => "Append Lighttpd redirect",
            perms => mog("0644","root","root"),
            create => "true",
            edit_line => append_if_no_lines('url.redirect = ("^/$" => "Tiny-Tiny-RSS-1.8/" )'),
            classes => if_repaired("reload_lighttpd"); 

commands:
    reload_lighttpd::
        "/etc/init.d/lighttpd force-reload";
}

bundle agent fix_lighttpd {
vars:
    any::
        "packages[lighttpd]"    string => "/usr/sbin/lighttpd";
        "packages[php5-cgi]"    string => "/usr/bin/php5-cgi";
        "packages[php5-mysql]"  string => "/usr/share/doc/php5-mysql/copyright";
        "packages[php5-curl]"   string => "/usr/share/doc/php5-curl/copyright";
        "packages[php5-cli]"    string => "/usr/bin/php";
        
        "packagesnames" slist => getindices("packages");

methods:
    any::
        "installpackages" usebundle => pkg_install_if_no_file(
                "$(packagesnames)", "$(packages[$(packagesnames)])");

classes:

    "installed_lighttpd" expression => fileexists("/usr/sbin/lighttpd");
    "installed_php5cgi" expression => fileexists("/usr/bin/php5-cgi");
    "installed_php5cli" expression => fileexists("/usr/bin/php");
    "installed_php5mysql" expression => fileexists("/usr/share/doc/php5-mysql/copyright");
    
    installed_lighttpd::
        "enabled_php5cgi" expression => fileexists("$(fascgiphp_conf)");
        "enabled_fastcgi" expression => fileexists("$(fascgi_conf)");

vars:
    "fascgiphp_conf" string => "/etc/lighttpd/conf-enabled/15-fastcgi-php.conf";
    "fascgi_conf" string => "/etc/lighttpd/conf-enabled/10-fastcgi.conf";

files:
    installed_lighttpd::
        "/etc/lighttpd/lighttpd.conf"
            handle => "fix_lighttpd_conf",
            comment => "Fix Lighttpd config file",
            perms => mog("0644","root","root"),
            edit_line => replace_line_end(
                    "static-file.exclude-extensions", 
                    '= ( ".pl", ".fcgi" )'),
            classes => if_repaired("reload_lighttpd"); 

commands:
    reload_lighttpd::
        "/etc/init.d/lighttpd force-reload";

    !enabled_fastcgi::
        "/usr/sbin/lighty-enable-mod fastcgi"
            classes => if_repaired("reload_lighttpd");

    !enabled_php5cgi::
        "/usr/sbin/lighty-enable-mod fastcgi-php"
            classes => if_repaired("reload_lighttpd"); 

}

bundle agent tinytinyrss_tarball_extract {
vars:
    "ttrss_tarball" string => "/root/ttrss-latest.tar.gz";
    "ttrss_destination" string => "/var/www";
    "ttrss_directory" string => "$(ttrss_destination)/Tiny-Tiny-RSS-1.8";

classes:
    "ttrss_directory_present"  expression => fileexists("$(ttrss_directory)/");
    "ttrss_tarball_present"  expression => fileexists("$(ttrss_tarball)");

files:
    "$(ttrss_directory)"
        perms => mog("0644","www-data","www-data"),
        depth_search => recurse("inf");

reports:
    ttrss_directory_present::
        "Ttrss directory is present.";

commands:
    !ttrss_directory_present.ttrss_tarball_present::
        "/bin/tar -C $(ttrss_destination) -xvzf $(ttrss_tarball)"
            comment => "Unrolling tarball to $(ttrss_destination).";

    ttrss_directory_present.(Q1|Q3)::
        "/usr/bin/php $(ttrss_directory)/update.php --feeds --quiet"
            comment => "Updating tinytinyrss",
            contain => in_dir_and_setuid_sh("/tmp", "www-data"),
            action => if_elapsed("30");
}

bundle agent configuration_of_mysql_db_for_ttrss
{
vars:
    "database" string => "ttrss";
    "user" string => "myttrssuser";
    "password" string => "TinyRSS@PassWord";

commands:
    "/usr/bin/mysql -u root -e \"
              CREATE DATABASE IF NOT EXISTS ttrss;
          GRANT ALL PRIVILEGES ON $(database).*
                  TO '$(user)'@localhost
                      IDENTIFIED BY '$(password)';
              FLUSH PRIVILEGES;\"
                    ";
}
