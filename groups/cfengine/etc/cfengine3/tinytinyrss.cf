bundle agent app_ttrss {
methods:
    tinytinyrss::
        "get_ttrss" usebundle => tinytinyrss_tarball_is_present;
        "handle_lighttpd" usebundle => fix_lighttpd;
}

bundle agent fix_lighttpd {
classes:

    "installed_lighttpd" expression => fileexists("/usr/sbin/lighttpd");
    "installed_php5cgi" expression => fileexists("/usr/bin/php5-cgi");
    "not_installed_lighttpd" not => "installed_lighttpd";
    "not_installed_php5cgi" not => "installed_php5cgi";
    
    installed_lighttpd::
        "enabled_php5cgi" expression => fileexists("$(fascgiphp_conf)");
        "enabled_fastcgi" expression => fileexists("$(fascgi_conf)");

vars:
        "fascgiphp_conf" string => "/etc/lighttpd/conf-enabled/15-fastcgi-php.conf";
        "fascgi_conf" string => "/etc/lighttpd/conf-enabled/10-fastcgi.conf";
        "lighttpdutils" slist => { "lighttpd", "php5-cgi" };

files:
    installed_lighttpd::
        "/etc/lighttpd/lighttpd.conf"
            handle => "fix_lighttpd_conf",
            comment => "Fix Lighttpd config file",
            perms => mog("0644","root","root"),
            edit_line => replace_line_end(
                    "static-file.exclude-extensions", 
                    '= ( ".pl", ".fcgi" )'),
            classes => if_repaired("reload_lighttpd"); 


packages:
    not_installed_lighttpd|not_installed_php5cgi::
        "$(lighttpdutils)"
        comment => "install $(lighttpdutils)",
                action => if_elapsed("240"),
                package_policy   => "add",
                package_method   => generic;

commands:
    reload_lighttpd::
        "/etc/init.d/lighttpd force-reload";

    !enabled_fastcgi::
        "/usr/sbin/lighty-enable-mod fastcgi"
            classes => if_repaired("reload_lighttpd");

    !enabled_php5cgi::
        "/usr/sbin/lighty-enable-mod fastcgi-php"
            classes => if_repaired("reload_lighttpd"); 

}

bundle agent tinytinyrss_tarball_is_present {
vars:
    "ttrss_tarball" string => "/root/ttrss-latest.tar.gz";
    "ttrss_url" string => "https://github.com/gothfox/Tiny-Tiny-RSS/archive/1.8.tar.gz";

classes:
      "ttrss_tarball_is_present" expression =>
              fileexists("$(ttrss_tarball)");

reports:
        ttrss_tarball_is_present::
                "TinytinyRSS tarball is on disk.";

commands:
          !ttrss_tarball_is_present::
                  "/usr/bin/wget -q -O $(ttrss_tarball) $(ttrss_url)"
                      comment => "Downloading WordPress.";
}
