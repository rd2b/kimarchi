bundle agent app_ttrss {
classes:
    tinytinyrss::
        "installed_lighttpd" expression => fileexists("/usr/sbin/lighttpd");
        "installed_php5cgi" expression => fileexists("/usr/bin/php5-cgi");
        "not_installed_lighttpd" not => "installed_lighttpd";
        "not_installed_php5cgi" not => "installed_php5cgi";
    
    tinytinyrss.installed_lighttpd::
        "enabled_php5cgi" expression => fileexists("$(fascgiphp_conf)");
        "enabled_fastcgi" expression => fileexists("$(fascgi_conf)");

vars:
    tinytinyrss::
        "fascgiphp_conf" string => "/etc/lighttpd/conf-enabled/15-fastcgi-php.conf";
        "fascgi_conf" string => "/etc/lighttpd/conf-enabled/10-fastcgi.conf";
        "lighttpdutils" slist => { "lighttpd", "php5-cgi" };

files:
    tinytinyrss.installed_lighttpd::
        "/etc/lighttpd/lighttpd.conf"
            handle => "fix_lighttpd_conf",
            comment => "Fix Lighttpd config file",
            perms => mog("0644","root","root"),
            edit_line => replace_line_end(
                    "static-file.exclude-extensions", 
                    '= ( ".pl", ".fcgi" )'),
            classes => if_repaired("reload_lighttpd"); 

packages:
    not_installed_lighttpd|not_installed_php5cgi::
        "$(lighttpdutils)"
        comment => "install $(lighttpdutils)",
                action => if_elapsed("240"),
                package_policy   => "add",
                package_method   => generic;

commands:
    reload_lighttpd::
        "/etc/init.d/lighttpd force-reload";

    !enabled_php5cgi::
        "/usr/sbin/lighty-enable-mod fastcgi"
            classes => if_repaired("reload_lighttpd");

        "/usr/sbin/lighty-enable-mod fastcgi-php"
            classes => if_repaired("reload_lighttpd"); 
}

