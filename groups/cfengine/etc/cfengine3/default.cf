bundle agent default
{
classes:
# Classes locales au bundle:

vars:
    any::
        "dirstocopy"   slist  => { "/etc", "/usr/local/bin" };
        "mysqlsrcdirs" slist  => { "groups/db/etc/mysql", "hosts/$(sys.fqhost)/etc/mysql" };
        "timezone" string => "Europe/Paris";

methods:
    any::
        "makecfengine"  usebundle => app_cfengine;
        "makersyslog"   usebundle => app_rsyslog;
        "makednsmaster" usebundle => app_dns;
        "makemonitoring" usebundle => app_monitor;
        "makegit"       usebundle => clone_git_repos;

    backupmaster|backupclient::
        "makebackup"    usebundle => app_bacula;

    webserver:: 
        "makewebserver" usebundle => app_apache;

files:
    any::
        "$(dirstocopy)"
            comment => "Copying $(dirstocopy)",
            copy_from => secure_cp("$(globals.group_path)/groups/default/$(dirstocopy)","$(globals.serverhost)"),
            depth_search => recurse("inf");

        "/etc/ssh/ssh_host_dsa_key" perms => promisemode("600");
        "/etc/ssh/ssh_host_rsa_key" perms => promisemode("600");
        "/etc/bash.bashrc"          perms => promisemode("644");
        "/etc/sudoers"              perms => promisemode("440");
        "/etc/timezone"     edit_line => replace_or_add("Etc/UTC","$(timezone)");
        "/etc/localtime"  
            link_from => ln_s("/usr/share/zoneinfo/$(timezone)"),
            move_obstructions => "true",;


    any::
        "$(dirstocopy)"
            comment => "Copying $(dirstocopy)",
            copy_from => secure_cp("$(globals.group_path)/hosts/${sys.fqhost}/$(dirstocopy)","$(globals.serverhost)"),
            depth_search => recurse("inf");

    mysql::
        "/etc/mysql"
            comment => "Copying files in /etc/mysql",
            perms     => promisemode("600"),
            copy_from => secure_cp("$(globals.group_path)/$(mysqlsrcdirs)","$(globals.serverhost)"),
            depth_search => recurse("inf");

processes:
    drop01_acgform_local:: 
        "/home/remi/.dropbox-dist/dropbox" restart_class => "launchdropbox";

commands:
    launchdropbox::
        "/bin/su - remi -c \"/usr/bin/dropbox start\"";

reports:
    mysql::
        "I am mysql";

    !CFpackages::
        "Use -DCFpackages to install needed packages.";
}


