bundle agent default
{
classes:
# Classes locales au bundle:

vars:
    any::
        "dirstocopy"   slist  => { "/etc", "/usr/local/bin" };
        "mysqlsrcdirs" slist  => { "groups/db/etc/mysql", "hosts/$(sys.fqhost)/etc/mysql" };
        "torrentsrc" slist  => { "groups/torrent/etc/transmission-daemon/settings.json" };
        "timezone" string => "Europe/Paris";


        "myuser" string => "remi"; 
        "transmissionuser" string => "debian-transmission";

        "keys" slist => {
            "ssh-rsa AAAAB3NzaC1yc2EAAAABIwAAAQEAxua5ofrJjw8VxBWv2jPqQR3bM47b7p9dhndE6PzHSt5NSOu43MSuMN8mI0jeax39UduwMqXg9ytyHmkJACv7QdhK4yS0lXgSydn/KgbO854JFgOeigfS8L3DSWTwU3kHSmWLDWnW9fp+y+JGUFSv10KoKZLkUaSdin+kF4EpwsnqxPd9e3Izm2V4T8kIkDd5CJD8n2Nw6gHeRv43PPf7Myd49AlOw44tp/c40XyL33R/Zy+Vg0GV1EAbRd3iT4tOGEfA5PVv5bJxFkJpRmLt2p9oIMEI31BPWJjCSCW25A/kyOwtBcKapy/H16H/EV9x32811uCIy/Wfk/FHzS+ZbQ== ", 
            "ssh-rsa AAAAB3NzaC1yc2EAAAADAQABAAABAQDUXjljNnCiBqm3Ycqt/jePhBe1jPE52GRHkF0ogVIVZWw+e8kemJNkqAETYGjZNv6cggO/P9GjZiDnSkjRENSpwPYJoUGU6/pa/Dd45yHAPGH7mM9Wbiegr8aepA1WeT9h0D9+cdFzDAsHN2EZ4DPHGpJ8FpcQ01K/TBvj9cwtHPKZnAEqGq/Pocq89QUdmIq4ASQBQgnR8xINYRp+5Yl9UJCsFQ7fOPgm1k5xv4sCMrBStOEImwT8X3MC6Hzr6ZUnfszgt7ZnyDv5vomMw6zIVhzRnFnSoHxmfD4jXRtcupEsK+nTKSv6657VTobB4URri9uovKqx1OPRHjxZvnHN remi@raspberrypi",
            "ssh-rsa AAAAB3NzaC1yc2EAAAADAQABAAABAQDCxJ6Cv0buzeiTKNyVdorYncXwzD93IB1jaM2jzlpNnjtfJjeVRQ6GdX/QTCAwiYJQZYB0bXgT6MaZCKUr80AXf8XxeDbdGQ8vA2nteMpFgOvwzxuq3AbaSftysPV5TYdvVnmoNKu4cA+i0fU1Z4wO3gm422kOGaF74BilWA1GB9313VDhF6dgbyNu+XT7EeOwR/IOpfdV6TDB70Q+JKhB6kJcp+8dwdWPjXY1/wOVWmve0mpMx+XjGOwhYcWoTcpkALL0D0bKFety6fBMcmmkBQjVEjSWdlbfMR66PxLXUXzoPuJipAe8tUjZ+m93JccPdURnEkpl89QZiievoMFH"
         };

methods:
    any::
        "makecfengine"  usebundle => app_cfengine;
        "makersyslog"   usebundle => app_rsyslog;
        "makednsmaster" usebundle => app_dns;
        "makemonitoring" usebundle => app_monitor;
        "makegit"       usebundle => clone_git_repos;

    backupmaster|backupclient::
        "makebackup"    usebundle => app_bacula;

    webserver:: 
        "makewebserver" usebundle => app_apache;

files:
    any::
        "$(dirstocopy)"
            comment => "Copying $(dirstocopy)",
            handle => "file_default",
            copy_from => secure_cp("$(globals.group_path)/groups/default/$(dirstocopy)","$(globals.serverhost)"),
            depth_search => recurse("inf");

        "/etc/ssh/ssh_host_dsa_key" perms => promisemode("600");
        "/etc/ssh/ssh_host_rsa_key" perms => promisemode("600");
        "/etc/bash.bashrc"          perms => promisemode("644");
        "/etc/sudoers"              perms => promisemode("440");

# Handling ssh authorized keys:
        "/home/${myuser}/.ssh/authorized_keys"
            handle => "remi_ssh_pub",
            comment => "Install public ssh keys",
            perms => mog("0644","${myuser}","${myuser}"),
            create =>  "true",
            edit_line => append_if_no_lines( "${keys}" );

       "/root/.ssh/authorized_keys"
            handle => "root_ssh_pub",
            comment => "Install root public ssh keys",
            perms => mog("0644","root","root"),
            create =>  "true",
            edit_line => append_if_no_lines( "${keys}" );

    gitserver::
        "/home/git/.ssh/authorized_keys"
            handle => "git_ssh_pub",
            comment => "Install public ssh keys",
            perms => mog("0644","git","git"),
            create =>  "true",
            edit_line => append_if_no_lines( "${keys}" );

    dlnaserver::
        "/etc/minidlna.conf"
            handle => "file_minidlna",
            perms => mog("0644","minidlna","minidlna");

    torrentserver::
        "/etc/transmission-daemon"
            handle => "file_transmission_directory",
             perms => mog("0755","${transmissionuser}","${transmissionuser}");

        "/home/Downloads/"
            perms => mog("0755","${transmissionuser}","${transmissionuser}"),
            create =>  "true";

        "/home/Incomplete/"
            perms => mog("0755","${transmissionuser}","${transmissionuser}"),
            create =>  "true";

    torrentserver.transmission_stopped::
        "/etc/transmission-daemon/settings.json"
            handle => "file_transmission",
            copy_from => secure_cp("$(globals.group_path)/$(torrentsrc)","$(globals.serverhost)"),
            classes => if_repaired("starttransmission"),
            perms => mog("0644","${transmissionuser}","${transmissionuser}");



    debian::
        "/etc/timezone"     
            handle => "file_timezone",
        edit_line => replace_or_add("Etc/UTC","$(timezone)");
        "/etc/localtime"  
            handle => "file_localtime",
            link_from => ln_s("/usr/share/zoneinfo/$(timezone)"),
            move_obstructions => "true";

        "/etc/inittab"
            handle => "file_inittab",
            edit_line => comment_lines_matching(
                    "[2-6]:23:respawn:/sbin/getty [0-9]* tty.*", "#");

    any::
        "$(dirstocopy)"
            comment => "Copying $(dirstocopy)",
            copy_from => secure_cp("$(globals.group_path)/hosts/${sys.fqhost}/$(dirstocopy)","$(globals.serverhost)"),
            depth_search => recurse("inf");

    mysql::
        "/etc/mysql"
            handle => "file_mysql",
            comment => "Copying files in /etc/mysql",
            perms     => promisemode("600"),
            copy_from => secure_cp("$(globals.group_path)/$(mysqlsrcdirs)","$(globals.serverhost)"),
            depth_search => recurse("inf");


processes:
    drop01_acgform_local:: 
        "/home/remi/.dropbox-dist/dropbox" restart_class => "launchdropbox";

    torrentserver::
        "/usr/bin/transmission-daemon" restart_class => "transmission_stopped";

commands:
    launchdropbox::
        "/bin/su - remi -c \"/usr/bin/dropbox start\"";

    starttransmission::
        "/etc/init.d/transmission-daemon start";
    stoptransmission::
        "/etc/init.d/transmission-daemon stop";

reports:
    mysql::
        "I am mysql";

    !CFpackages::
        "Use -DCFpackages to install needed packages.";
}


