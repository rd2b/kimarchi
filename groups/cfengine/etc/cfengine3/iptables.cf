bundle agent app_iptables
{
classes:
    "iptables_enabled" or => {"ks01", "ks02", "ks03"};

vars:
    iptables_enabled::
        "iptables_dir" string => "/etc/iptables.d/.";
        "iptables_blacklist" string => "/usr/local/sbin/blacklist-iptables.sh";
        "scanip_url" string => "http://www.openbl.org/lists/base_30days.txt.gz";
        "scanip_file" string => "/root/blacklist.txt.gz";
        "scanip_destination" string => "/root/blacklist.txt";

        "blacklist" slist => readstringlist(
                "$(scanip_destination)",
                "\s*#[^\n]*", "\n", 1000000, 1000000000);           

methods:
    iptables_enabled.once_per_hour::
        "get_scanip" usebundle => download_tarball(
                "$(scanip_url)", "$(scanip_file)");

files:
    iptables_enabled::
        "$(iptables_dir)/." 
            create => "true",
            perms => owner("root");

        "$(scanip_destination)" create => "true", perms => promisemode("644");
        "$(scanip_file)"        create => "true", perms => promisemode("644");
        "$(iptables_blacklist)"       create => "true", perms => mog("0755", "root", "root");


commands:

    iptables_enabled.once_per_hour::
       "/bin/gunzip -f $(scanip_file)"
            comment => "Unrolling tarball to $(scanip_destination).";

    iptables_enabled::
        '/bin/sed -n "s/^\([0-9\.]*\)$/\/sbin\/iptables -A INPUT -s \1 -j DROP/p" < $(scanip_destination) > $(iptables_blacklist)'
            contain => in_shell,
            action => if_elapsed("120");

}


