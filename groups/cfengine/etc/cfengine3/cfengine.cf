bundle agent app_cfengine
{
vars:
    "masterfilesrcdir" string => "$(globals.group_path)/groups/cfengine/etc/cfengine3/";

    ubuntu|debian::
        "defaultdirfile" string => "$(globals.group_path)/groups/cfengine/etc/default/";

    (ubuntu|debian).cfmaster::
        "defaultsrcfile" string => "$(defaultdirfile)/cfengine3-master";

    (ubuntu|debian).!cfmaster.!cfmanual::
        "defaultsrcfile" string => "$(defaultdirfile)/cfengine3-client";

    (ubuntu|debian).cfmanual::
        "defaultsrcfile" string => "$(defaultdirfile)/cfengine3-manual";
    
    ubuntu|debian::
        "master_location"   string => "/var/lib/cfengine3/masterfiles";
    
    redhat::
        "master_location"   string => "/var/cfengine/masterfiles";              
files:
    (ubuntu|debian).cfmanual::
        "/etc/default/cfengine3"
                  comment   => "copy default/cfengine3 to /etc",
                  perms     => promisemode("644"),
                  copy_from => secure_cp("$(defaultsrcfile)","$(globals.serverhost)");

    (ubuntu|debian).!cfmanual::
        "/etc/default/cfengine3"
            comment  => "Adding start cf-execd",
            edit_line => append_if_no_line("RUN_CFEXECD=1");


    (ubuntu|debian|redhat).(cfmaster|cfmanual)::
        "$(master_location)"
        comment => "Updating masterfiles",
        copy_from => secure_cp("$(masterfilesrcdir)","$(globals.serverhost)"),
        file_select => by_name(".*.cf"),
        depth_search => recurse("inf");

processes:
    !cfmanual:: 
        "/usr/sbin/cf-execd" restart_class => "startcfexecd";

    cfmaster|cfmanual::
        "/usr/sbin/cf-serverd" restart_class => "startcfserverd";

commands:
    startcfexecd.(ubuntu|debian)::
        "/etc/init.d/cfengine3 start";

    startcfexecd.redhat::
        "/etc/init.d/cf-execd start";

    startcfserverd.redhat::
        "/etc/init.d/cf-serverd start";

reports:
    cfmaster::
        "I am cfengine master";
    cfmanual::
        "I am standalone master";
    !cfmaster.!cfmanual::
        "I am cfengine slave";

}

