bundle agent app_cfengine
{
vars:
    "masterfilesrcdir" string => "$(globals.group_path)/groups/cfengine/etc/cfengine3/";

    ubuntu|debian::
        "defaultdirfile" string => "$(globals.group_path)/groups/cfengine/etc/default/";

    ubuntu|debian::
        "master_location"   string => "/var/lib/cfengine3/masterfiles";
    
    redhat|gentoo::
        "master_location"   string => "/var/cfengine/masterfiles";              

files:

    (ubuntu|debian|redhat|gentoo).(cfmaster|cfmanual)::
        "$(master_location)"
        comment => "Updating masterfiles",
        copy_from => secure_cp("$(masterfilesrcdir)","$(globals.serverhost)"),
        file_select => by_name(".*.cf"),
        depth_search => recurse("inf");

    # Recuperation du depot principal:
    cfmaster|cfmanual::
        "/data/cf-repos"
        handle => "update_link_repository",
        link_from => ln_s("/data/cfexternals/kimarchi"),
        move_obstructions => "true";

processes:
    !cfmanual:: 
        "/usr/sbin/cf-execd" restart_class => "startcfexecd";

    cfmaster|cfmanual::
        "/usr/sbin/cf-serverd" restart_class => "startcfserverd";

commands:
    restartcfengine.(ubuntu|debian)::
        "/etc/init.d/cfengine3 start";

    startcfexecd.(ubuntu|debian)::
        "/etc/init.d/cfengine3 start";

    startcfexecd.redhat::
        "/etc/init.d/cf-execd start";

    startcfserverd.redhat::
        "/etc/init.d/cf-serverd start";

}

