bundle agent app_cfengine
{
vars:
    "masterfilesrcdir" string => "$(globals.group_path)/groups/cfengine/etc/cfengine3/";

    ubuntu::
        "defaultdirfile" string => "$(globals.group_path)/groups/cfengine/etc/default/";

    ubuntu.cfmaster::
        "defaultsrcfile" string => "$(defaultdirfile)/cfengine3-master";

    ubuntu.!cfmaster.!cfmanual::
        "defaultsrcfile" string => "$(defaultdirfile)/cfengine3-client";

    ubuntu.cfmanual::
        "defaultsrcfile" string => "$(defaultdirfile)/cfengine3-manual";
    
    ubuntut::
        "master_location"   string => "/var/lib/cfengine3/masterfiles";
    
    redhat::
        "master_location"   string => "/var/cfengine/masterfiles";              
files:
    ubuntu::
        "/etc/default/cfengine3"
                  comment   => "copy default/cfengine3 to /etc",
                  perms     => promisemode("644"),
                  copy_from => secure_cp("$(defaultsrcfile)","$(globals.serverhost)");

    ubuntu||redhat::
        "$(master_location)"
        comment => "Updating masterfiles",
        copy_from => secure_cp("$(masterfilesrcdir)","$(globals.serverhost)"),
        depth_search => recurse("inf");

processes:
    !cfmanual:: 
        "/usr/sbin/cf-execd" restart_class => "startcfexecd";

    cfmaster||cfmanual::
        "/usr/sbin/cf-serverd" restart_class => "startcfserverd";

commands:
    startcfexecd.ubuntu::
        "/etc/init.d/cfengine3 start";

    startcfexecd.redhat::
        "/etc/init.d/cf-execd start";

    startcfserverd.redhat::
        "/etc/init.d/cf-serverd start";

reports:
    cfmaster::
        "I am cfengine master";
    cfmanual::
        "I am standalone master";
    !cfmaster.!cfmanual::
        "I am cfengine slave";

}

